;
; flow.qdot -- QDot flow-control;
;
; Copyright (C) 2015 Bruno F‚lix Rezende Ribeiro <oitofelix@gnu.org>
;
; This program is free software; you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation; either version 3, or (at your option)
; any later version.
;
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.
;

%ifndef QDOT_FLOW_QDT
%define QDOT_FLOW_QDT
%push QDOT_FLOW_QDT





;;;;;;
; ?if
;;;;;;

%macro ?if 1+
  %push ?if
  %assign %$func_numargs %$$func_numargs
  %assign %$func_numlocals %$$func_numlocals
  %assign %$flow_i 0
  ? %1
  pop ax
  cmp ax, ?TRUE
  jne %$qdot_cond_%[%$flow_i]
%endmacro


;;;;;;;;;;
; ?elseif
;;;;;;;;;;

%macro ?elseif 1+
  %ifctx ?if
    jmp %$qdot_cond_end
    %$qdot_cond_%[%$flow_i]:
      %assign %$flow_i %$flow_i + 1
      ? %1
      pop ax
      cmp ax, ?TRUE
      jne %$qdot_cond_%[%$flow_i]
  %else
    %error "Expected '?if' before '?elseif'"
  %endif
%endmacro


;;;;;;;;
; ?else
;;;;;;;;

%macro ?else 0
  %ifctx ?if
    jmp %$qdot_cond_end
    %$qdot_cond_%[%$flow_i]:
      %assign %$flow_i %$flow_i + 1
  %else
    %error "Expected '?if' before '?else'"
  %endif
%endmacro


;;;;;;;;;
; ?endif
;;;;;;;;;

%macro ?endif 0
  %$qdot_cond_end:
  %$qdot_cond_%[%$flow_i]:
  %pop ?if
%endmacro





;;;;;;;;;;
; ?switch
;;;;;;;;;;

%macro ?switch 0-1+
  %push ?switch
  %assign %$func_numargs %$$func_numargs
  %assign %$func_numlocals %$$func_numlocals
  %assign %$flow_i 0
  ? %1
  jmp %$qdot_cond_%[%$flow_i]
%endmacro


;;;;;;;;;;
; ?case
;;;;;;;;;;

%macro ?case 1+
  %ifctx ?switch
    jmp %$qdot_cond_end
    %$qdot_cond_%[%$flow_i]:
      %assign %$flow_i %$flow_i + 1
      ? %1
      pop ax
      cmp ax, ?TRUE
      jne %$qdot_cond_%[%$flow_i]
  %else
    %error "Expected '?switch' before '?case'"
  %endif
%endmacro


;;;;;;;;;;;
; ?default
;;;;;;;;;;;

%macro ?default 0
  %ifctx ?switch
    jmp %$qdot_cond_end
    %$qdot_cond_%[%$flow_i]:
      %assign %$flow_i %$flow_i + 1
  %else
    %error "Expected '?switch' before '?default'"
  %endif
%endmacro


;;;;;;;;;;;;;
; ?endswitch
;;;;;;;;;;;;;

%macro ?endswitch 0
  %$qdot_cond_end:
  %$qdot_cond_%[%$flow_i]:
  %pop ?switch
%endmacro





;;;;;;
; ?do
;;;;;;

%macro ?do 0
  %push ?do
  %assign %$func_numargs %$$func_numargs
  %assign %$func_numlocals %$$func_numlocals
  %ifnempty %00
    %xdefine %$qdot_loop_name %00
  %endif
  %$qdot_begin_loop:
  %ifnempty %$qdot_loop_name
    %[%$qdot_loop_name]_continue:
  %endif
%endmacro


;;;;;;;;;
; ?while
;;;;;;;;;

%macro ?while 1+
  %ifctx ?do
    ? %1
    pop ax
    cmp ax, ?TRUE
    je %$qdot_begin_loop
    %$qdot_end_loop:
    %ifnempty %$qdot_loop_name
      %[%$qdot_loop_name]_break:
    %endif
    %pop ?do
  %else
    %push ?while
    %assign %$func_numargs %$$func_numargs
    %assign %$func_numlocals %$$func_numlocals
    %ifnempty %00
      %xdefine %$qdot_loop_name %00
    %endif
    %$qdot_begin_loop:
    %ifnempty %$qdot_loop_name
      %[%$qdot_loop_name]_continue:
    %endif
      ? %1
      pop ax
      cmp ax, ?FALSE
      je %$qdot_end_loop
  %endif
%endmacro


;;;;;;;;;;;;
; ?endwhile
;;;;;;;;;;;;

%macro ?endwhile 0
  jmp %$qdot_begin_loop
  %$qdot_end_loop:
  %ifnempty %$qdot_loop_name
    %[%$qdot_loop_name]_break:
  %endif
  %pop ?while
%endmacro





;;;;;;;;;;
; ?for
;;;;;;;;;;

%macro ?for 0-1+
  %push ?for
  %assign %$func_numargs %$$func_numargs
  %assign %$func_numlocals %$$func_numlocals
  %ifnempty %00
    %xdefine %$qdot_loop_name %00
  %endif
  ? %1
  %$qdot_for:
%endmacro


;;;;;;;;;;
; ?cond
;;;;;;;;;;

%macro ?cond 1+
  %ifctx ?for
      ? %1
      pop ax
      cmp ax, ?TRUE
      jne %$qdot_end_loop
  %else
    %error "Expected '?for' before '?cond'"
  %endif
%endmacro


;;;;;;;;;;
; ?next
;;;;;;;;;;

%macro ?next 0-1+
  %$qdot_begin_loop:
  %ifnempty %$qdot_loop_name
    %[%$qdot_loop_name]_continue:
  %endif
  ? %1
  jmp %$qdot_for
  %$qdot_end_loop:
  %ifnempty %$qdot_loop_name
    %[%$qdot_loop_name]_break:
  %endif
  %pop ?for
%endmacro





;;;;;;;;;;
; ?break
;;;;;;;;;;

%macro ?break 1
  jmp %1_break
%endmacro


;;;;;;;;;;;;
; ?continue
;;;;;;;;;;;;

%macro ?continue 1
  jmp %1_continue
%endmacro





%pop QDOT_FLOW_QDT
%endif ; QDOT_FLOW_QDT

; Local Variables:
; coding: cp437-dos
; End:
